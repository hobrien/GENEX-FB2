from GetSequences import get_sequences
configfile: "config.yaml"

files=get_sequences(config['seqfile'])

rule all:
    input:
        expand("GTcheck/{sample}.png", sample=files.keys())

rule hisat:
    input:
        reads = lambda wildcards: files[wildcards.sample]
    output:
        temp("BAM/{sample}.bam")
    params:
        input1 = lambda wildcards: ','.join(files[wildcards.sample][::2]),
        input2 = lambda wildcards: ','.join(files[wildcards.sample][1::2]),        
        idx = config['reference']['index'],
        extra = '--known-splicesite-infile ' + config['reference']['splice_sites'],
        threads = 8
    benchmark:
        "Benchmarks/{sample}.hisat.benchmark.txt"
    log:
        "Logs/{sample}_hisat_map.txt"
    shell:
        "(hisat2 {params.extra} --threads {params.threads}"
        " -x {params.idx} -1 {params.input1} -2 {params.input2}"
        " | samtools view -Sbh -o {output} -)"
        " 2> {log}"


rule sort_bam:
    input:
        rules.hisat.output
    output:
        "BAM/{sample}.sort.bam"
    params:
        "-m 4G"
    threads: 8
    wrapper:
        "0.17.4/bio/samtools/sort"
        
rule samtools_index:
    input:
        rules.sort_bam.output
    output:
        "BAM/{sample}.sort.bam.bai"
    wrapper:
        "0.17.4/bio/samtools/index"

rule call_snps:
    input:
        bam=rules.sort_bam.output,
        index=rules.samtools_index.output
    output:
        "SNPcalls/{sample}.vcf.gz"
    params:
        fasta = config['reference']['genome']
    shell:
        "samtools mpileup -uf {params.fasta} {input.bam} | "
        "bcftools call -mv -Oz > {output}"

rule index_vcf:
    input:
        rules.call_snps.output
    output:
        "SNPcalls/{sample}.vcf.gz.tbi"
    shell:
        "bcftools index {input}"

rule gt_check:
    input:
        vcf=rules.call_snps.output,
        index=rules.index_vcf.output
    output:
        "GTcheck/{sample}.png" # No idea what this will be called
    params:
        sample = "{sample}",
        prefix = "GTcheck/{sample}",
        genotyping="Genotypes/Combined/combined_filtered.vcf.gz"
    shell:
        "bcftools gtcheck -g {params.genotyping} -p {params.prefix} -S {params.sample} {input.vcf} "

